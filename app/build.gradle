// Copyright 2015 The Project Buendia Authors
//
// Licensed under the Apache License, Version 2.0 (the "License"); you may not
// use this file except in compliance with the License.  You may obtain a copy
// of the License at: http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software distrib-
// uted under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES
// OR CONDITIONS OF ANY KIND, either express or implied.  See the License for
// specific language governing permissions and limitations under the License.

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.14.1'
        classpath 'org.ajoberstar:gradle-git:0.8.0'
    }
}

apply plugin: 'com.android.application'
def appId = 'org.projectbuendia.client'
def debugAppIdSuffix = '.dev'
def debugAppId = appId + debugAppIdSuffix
def contentAuthority = appId + '.provider'
def debugContentAuthority = debugAppId + '.provider'

android {
    dexOptions {
        preDexLibraries = false
    }
}
dependencies {
    // Build plugins
    // TODO: Figure out why removing this causes a Dagger error.
    provided 'com.github.frankiesardo:auto-parcel-processor:0.2' // Automatic Parcelables
    provided 'com.squareup.dagger:dagger-compiler:1.2.2' // Dependency injection
    // provided 'com.stanfy.spoon:spoon-gradle-plugin:0.14.1' // Test multiplexing/selection

    // Local .jar dependencies
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Local project dependencies
    compile project(':third_party:odkcollect')
    compile project(':third_party:AutoFitTextView')

    // External dependencies
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.android.support:cardview-v7:21.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.android.support:support-v4:21.0.0'
    compile 'com.github.zafarkhaja:java-semver:0.8.0' // Semantic versioning
    compile 'com.google.code.gson:gson:2.3' // JSON parser
    compile 'com.google.guava:guava:18.0' // Google common libraries
    compile 'com.jakewharton:butterknife:5.1.2' // View injection
    compile 'com.mcxiaoke.volley:library:1.0.6' // HTTP framework
    compile 'com.joanzapata.android:android-iconify:1.0.8' // Font-based icons
    compile 'com.squareup.dagger:dagger:1.2.2' // Dependency injection
    compile 'de.greenrobot:eventbus:2.4.0' // The event bus
    compile 'joda-time:joda-time:2.5' // Better dates and times

    // Testing
    androidTestCompile 'com.google.dexmaker:dexmaker:1.0'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0'
    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.google.guava'
        exclude group: 'com.squareup.dagger' // Provided by com.squareup.dagger
        exclude group: 'javax.inject' // Provided by com.squareup.dagger
    }
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.1'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    compile 'com.mitchellbosecke:pebble:1.5.1'
    compile 'org.slf4j:slf4j-simple:1.7.12'
}

apply plugin: 'spoon'

spoon {
    if (project.hasProperty('spoonClassName')) {
        className = project.spoonClassName
    }
}

// Consider spoon to always be "out-of-date" so that spoon tests will run (when requested) even
// when no changes have been made to the build.
tasks['spoon'].outputs.upToDateWhen { false }

// Development builds get the lowest possible version code, so that any release
// build will be considered an upgrade over any development build.
def versionInteger = 0

// Development builds get a version name that identifies the commit.
def repo = org.ajoberstar.grgit.Grgit.open(project.rootDir)
def versionString = repo.head().id.substring(0, 7) + (repo.status().clean ? '' : '+')

// For a release build, use -PversionNumber=1.2.3 to set the version number.
// It should have at most three parts, all numeric (e.g. 1, 1.2, or 1.2.3).
if (project.hasProperty('versionNumber')) {
    // The version number is converted to an integer for the version code,
    // e.g. 2.34.5 becomes 2340005.
    def versionParts = project.versionNumber.split('\\.').toList()
    assert versionParts.size >= 1 && versionParts.size() <= 3
    versionParts.addAll(['0', '0'])  // missing parts default to 0
    def versionMajor = versionParts[0].toInteger()
    def versionMinor = versionParts[1].toInteger()
    def versionPatch = versionParts[2].toInteger()
    assert versionMajor < 100
    assert versionMinor < 100
    assert versionPatch < 10000
    versionInteger = versionMajor * 1000000 + versionMinor * 10000 + versionPatch
    // Construct a normalized string for the version name, formatting integers
    // canonically and dropping trailing zeroes (e.g. '0.001.0' becomes '0.1').
    versionString = versionMajor + '.' + versionMinor + '.' + versionPatch;
    versionString = versionString.replaceAll('(\\.0)*$', '');
}
logger.info("Version number: ${versionString}")
logger.info("Version code: ${versionInteger}")

// This is where to look for the .apk signing key.
def releaseRepoRoot = file('../../release/')

// OpenMRS instance for development (kept at tip of dev branch).
def openmrsRootUrlDev = 'http://dev.projectbuendia.org:9000/openmrs'

// OpenMRS instance for integration testing server.
def openmrsRootUrlIntegration = 'http://integration.projectbuendia.org:9000/openmrs'

// OpenMRS root URL for Edison on local network (default for dev tablets).
def openmrsRootUrlEdison = 'http://server:9000/openmrs'

// OpenMRS root URL for host machine when client runs in an emulator (AVD).
def openmrsRootUrlLocalhost = 'http://10.0.2.2:9000/openmrs'

// Just for development.  In production builds, these should be overridden
// using -PopenmrsRootUrl=url -PopenmrsUser=user -PopenmrsPassword=pass etc.
def openmrsRootUrlDefault = openmrsRootUrlDev;
def openmrsUserDefault = 'buendia'
def openmrsPasswordDefault = 'buendia'
def packageServerRootUrlDefault = 'http://packages.projectbuendia.org:9001/'

// Apply overrides such as -PopenmrsRootUrl=url etc.
project.metaClass.propertyMissing = { null }
def openmrsRootUrl = project.openmrsRootUrl ?: openmrsRootUrlDefault
def openmrsUser = project.openmrsUser ?: openmrsUserDefault
def openmrsPassword = project.openmrsPassword ?: openmrsPasswordDefault
def packageServerRootUrl = project.packageServerRootUrl ?: packageServerRootUrlDefault

logger.info('Default preference values:')
logger.info("OpenMRS root URL: ${openmrsRootUrl}")
logger.info("OpenMRS user: ${openmrsUser}")
logger.info("OpenMRS password: ${openmrsPassword}")
logger.info("Package server root URL: ${packageServerRootUrl}")

android {
    compileSdkVersion 21
    buildToolsVersion '19.1.0'

    sourceSets.main {
        jniLibs.srcDir 'libs'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        applicationId appId
        minSdkVersion 19
        targetSdkVersion 19

        versionCode versionInteger
        versionName versionString
        resValue 'string', 'app_version', versionString

        // To run tests in Android Studio, you will need to open the run configuration and
        // select GoogleInstrumentationTestRunner as the "Specific instrumentation runner".
        testInstrumentationRunner 'com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner'
        testApplicationId appId + '.test'

        resValue 'string', 'openmrs_root_url_default', openmrsRootUrl
        resValue 'string', 'openmrs_user_default', openmrsUser
        resValue 'string', 'openmrs_password_default', openmrsPassword
        resValue 'string', 'package_server_root_url_default', packageServerRootUrl

        // Allow different sync adapters to be defined for debug and release builds.
        resValue 'string', 'content_authority', contentAuthority
        buildConfigField 'String', 'CONTENT_AUTHORITY', '"' + contentAuthority + '"'
        resValue 'string', 'account_type', appId
        buildConfigField 'String', 'ACCOUNT_TYPE', '"' + appId + '"'
    }

    signingConfigs {
        release {
            storeFile file(new File(releaseRepoRoot, 'keys/org.projectbuendia.client.jks'))
            storePassword ''
            keyAlias 'org.projectbuendia.client'
            keyPassword ''
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
    }

    lintOptions {
        lintConfig file('lint.xml')
        htmlReport false
    }

    buildTypes {
        debug {
            applicationIdSuffix debugAppIdSuffix
            resValue 'string', 'content_authority', debugContentAuthority
            buildConfigField 'String', 'CONTENT_AUTHORITY', '"' + debugContentAuthority + '"'
            resValue 'string', 'account_type', debugAppId
            buildConfigField 'String', 'ACCOUNT_TYPE', '"' + debugAppId + '"'
        }

        release {
            // noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
}

gradle.taskGraph.whenReady {
    taskGraph ->
        if (taskGraph.hasTask(':app:assembleRelease')) {
            if (!android.signingConfigs.release.storeFile.exists()) {
                throw new IllegalStateException("To perform a release build, you must have the release repo cloned at ${releaseRepoRoot}.")
            }
            if (System.console() == null) {
                throw new IllegalStateException("Assembling the release build only works from command line with the Gradle Daemon disabled. Please run ./gradlew :app:assembleRelease.");
            }
            def pass = new String(System.console().readPassword("\nPlease enter key passphrase: "))
            if (!pass) {
                throw new IllegalArgumentException("You must enter a password to proceed.")
            }
            android.signingConfigs.release.storePassword = pass
            android.signingConfigs.release.keyPassword = pass
        }
}
