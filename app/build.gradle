buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 0

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

dependencies {
    // Build plugins
    provided 'com.github.frankiesardo:auto-parcel-processor:+' // Automatic Parcelables
    provided 'com.squareup.dagger:dagger-compiler:1.2.2' // Dependency injection

    // Local .jar dependencies
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Local project dependencies
    compile project(':autofittextview')
    compile project(':odkcollect')
    compile project(':snackbar')
    compile project(':ViewPagerIndicator')

    // External dependencies
    compile 'com.android.support:appcompat-v7:+'
    compile 'com.android.support:cardview-v7:+'
    compile 'com.android.support:recyclerview-v7:+'
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.github.frankiesardo:auto-parcel:+' // Automatic Parcelables
    compile 'com.github.zafarkhaja:java-semver:0.8.0' // Semantic versioning
    compile 'com.google.code.gson:gson:2.3' // JSON parser
    compile 'com.google.guava:guava:18.0' // Google common libraries
    compile 'com.jakewharton:butterknife:5.1.2' // View injection
    compile 'com.mcxiaoke.volley:library:1.0.6' // HTTP framework
    compile 'com.squareup.dagger:dagger:1.2.2' // Dependency injection
    compile 'de.greenrobot:eventbus:2.4.0' // The event bus
    compile 'joda-time:joda-time:2.5' // Better dates and times

    // Testing
    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.squareup.dagger' // Provided by com.squareup.dagger
        exclude group: 'javax.inject' // Provided by com.squareup.dagger
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "19.1.0"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        applicationId "org.msf.records"

        minSdkVersion 19
        targetSdkVersion 20

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"

        // Allow different sync adapters to be defined for debug and release builds.
        resValue "string", "sync_authority", applicationId + '.provider.records'
        resValue "string", "account_type", applicationId
        buildConfigField "String", "ACCOUNT_TYPE", "\"${defaultConfig.applicationId}\""

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'

            resValue "string", "sync_authority", defaultConfig.applicationId + '.dev.provider.records'
            resValue "string", "account_type", defaultConfig.applicationId + '.dev'
            buildConfigField "String", "ACCOUNT_TYPE", "\"${defaultConfig.applicationId}.dev\""

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
    }

    lintOptions {
        abortOnError false
    }
}
